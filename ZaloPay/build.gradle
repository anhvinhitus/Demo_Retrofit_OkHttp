// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'

ext {
    configuration = [
            applicationId    : "vn.com.vng.zalopay",
            buildToolsVersion: "25.0.0",
            compileVersion   : 25,
            minSdk           : 16,
            targetSdk        : 23
    ]
}

buildscript {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$projectDir/../../maven_local"
        }
        maven {
            // repository for circular reveal in zalopay.sdk
            url "https://jitpack.io"
        }
        flatDir {
            dirs "../libs"
        }
    }
}

task runDomainUnitTests(dependsOn: [':zalopay.domain:test']) {
    description 'Run unit tests for the domain layer.'
}

task runDataUnitTests(dependsOn: [':zalopay.data:cleanTestDebugUnitTest', ':zalopay.data:testDebugUnitTest']) {
    description 'Run unit tests for the data layer.'
}

task runUnitTests(dependsOn: ['runDomainUnitTests', 'runDataUnitTests']) {
    description 'Run unit tests for both domain and data layers.'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}
