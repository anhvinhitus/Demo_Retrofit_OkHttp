apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.frogermcs.androiddevmetrics'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.4'
    }
}

def cfg = rootProject.ext.configuration

android {
    compileSdkVersion cfg.compileVersion
    buildToolsVersion cfg.buildToolsVersion

    defaultConfig {
        applicationId "vn.com.vng.zalopay"
        minSdkVersion cfg.minSdk
        targetSdkVersion cfg.targetSdk
        versionCode 1
        versionName "1.0.${versionCode}"
        multiDexEnabled true

        buildConfigField "String", "PRODUCTION_FLAVOR", "\"production\""
        buildConfigField "String", "BETA_FLAVOR", "\"beta\""
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../../publish/zalopay-debug.jks")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file("../../publish/zalopay-release.jks")
            storePassword KEYSTORE_PASSWORD
            keyAlias 'zalo@vng'
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false

            // removes all unused code - http://developer.android.com/tools/help/proguard.html
            shrinkResources true

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'HOST', '"https://sandbox.zalopay.pay.zing.vn/"'
            buildConfigField 'int', 'PAYAPPID', '1'

        }
        debug {
            minifyEnabled false
            //  applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
            buildConfigField 'String', 'HOST', '"https://zalopay.com.vn/"'
            buildConfigField 'int', 'PAYAPPID', '1'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/maven/com.google.zxing/core/pom.xml'
        exclude 'META-INF/maven/com.google.zxing/core/pom.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        production {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.RELEASE'
        }

        beta {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_INTERNAL'
        }

        iapbeta {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_EXTERNAL'
        }
    }



    lintOptions {

        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'LICENSE.txt'
        exclude 'META-INF/ASL2.0'

    }

    dexOptions {
        jumboMode = true

//        preDexLibraries = false
        javaMaxHeapSize "2g"
    }

    if (isCIBuild()) {
        // http://stackoverflow.com/questions/31191898/android-studio-gradle-dynamic-apk-name-out-of-sync/34095961#34095961
        // ./gradlew clean assemble -PCI_BUILD=true
        def clientBuildDate = new Date().format('yyyyMMddHHmm')
        def newName = defaultConfig.versionName.replace(".", "_") + "-" + clientBuildDate
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", "-" + newName + ".apk"));
            }
        }
    }
}

def isCIBuild() {
    return System.getenv('CI_BUILD') != null || hasProperty('CI_BUILD');
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd') //HHmmss
    return formattedDate
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += '--multi-dex'
        dx.additionalParameters += "--main-dex-list=$projectDir/zalopay".toString()
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def tests = rootProject.ext.testingLibraries
    def libs = rootProject.ext.libraries
    def devel = rootProject.ext.developmentDependencies;

    def cfgAndroid = rootProject.ext.configurationAndroid;

    testCompile tests.junit

    compile cfgAndroid.multidex

    compile libs.butterKnife
    apt libs.butterKnifeCompiler

    debugCompile devel.leakCanaryDebug
    releaseCompile devel.leakCanaryRelease

    compile libs.support_annotations
    compile libs.dagger
    apt libs.daggerCompiler
    provided libs.javaxAnnotation

    //  compile libs.play_services
    compile libs.play_services_analytics

    debugCompile project(':debugviewer')
    releaseCompile project(':debugviewer-release')

    compile project(':data')
    compile project(':uicomponent')
    compile project(':moduledynamicloading')
    compile project(':qrcodecore')

    compile project(':zalosdk')
    compile 'com.android.support:support-v4:23.4.0'
}
