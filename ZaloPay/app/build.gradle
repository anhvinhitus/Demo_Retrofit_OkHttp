    apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.4'
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates

        classpath 'io.fabric.tools:gradle:1.+'
    }
}

def cfg = rootProject.ext.configuration

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "http://dl.bintray.com/jjhesk/maven" }
}

android {
    compileSdkVersion cfg.compileVersion
    buildToolsVersion cfg.buildToolsVersion

    defaultConfig {
        applicationId "vn.com.vng.zalopay"
        minSdkVersion cfg.minSdk
        targetSdkVersion cfg.targetSdk
        versionCode 39
        versionName "2.0.19"

        multiDexEnabled true

        buildConfigField "boolean", "DOWNLOAD_APP_RESOURCE", "true"
        buildConfigField 'int', 'PAYAPPID', '1'
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    sourceSets {
        main {
            assets.srcDirs += '../../internalapps'
        }

        production {
            assets.srcDirs += '../../paymentapps/production'
        }

        sandbox {
            assets.srcDirs += '../../paymentapps/sandbox'
        }

        staging {
            assets.srcDirs += '../../paymentapps/staging'
        }

        internalProduction {
            assets.srcDirs += '../../paymentapps/production'
            res.srcDirs += 'src/production/res'

        }

        internalSandbox {
            assets.srcDirs += '../../paymentapps/sandbox'
            res.srcDirs += 'src/sandbox/res'
        }

        internalStaging {
            assets.srcDirs += '../../paymentapps/staging'
            res.srcDirs += 'src/staging/res'
        }

        iapProduction {
            assets.srcDirs += '../../paymentapps/production'
            res.srcDirs += 'src/production/res'
        }

        iapSandbox {
            assets.srcDirs += '../../paymentapps/sandbox'
            res.srcDirs += 'src/sandbox/res'
        }

        iapStaging {
            assets.srcDirs += '../../paymentapps/staging'
            res.srcDirs += 'src/staging/res'
        }
    }

    signingConfigs {
        debug {
            storeFile file("../../publish/zalopay-debug.jks")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file("../../publish/zalopay-release.jks")
            storePassword KEYSTORE_PASSWORD
            keyAlias 'zalo@vng'
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false

            // removes all unused code - http://developer.android.com/tools/help/proguard.html
            shrinkResources true

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        //    applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        production {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.RELEASE'
            buildConfigField 'String', 'HOST', '"https://zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.LIVE'
            buildConfigField 'String', 'WS_HOST', '"notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8414'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"http://upload.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://rpe.zalopay.com.vn/"'
        }

        sandbox {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.RELEASE'
            buildConfigField 'String', 'HOST', '"https://sandbox.zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.SANDBOX'
            buildConfigField 'String', 'WS_HOST', '"sandbox.notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8404'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"https://sandbox.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://sandbox.zalopay.com.vn/"'
        }

        staging {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.RELEASE'
            buildConfigField 'String', 'HOST', '"https://stg.zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.STAGING'
            buildConfigField 'String', 'WS_HOST', '"stg.notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8424'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"http://stgupload.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://stgrpe.zalopay.com.vn/"'
        }

        internalProduction {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_INTERNAL'
            buildConfigField 'String', 'HOST', '"https://zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.LIVE'
            buildConfigField 'String', 'WS_HOST', '"notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8414'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"http://upload.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://rpe.zalopay.com.vn/"'
        }

        internalSandbox {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_INTERNAL'
            buildConfigField 'String', 'HOST', '"https://sandbox.zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.SANDBOX'
            buildConfigField 'String', 'WS_HOST', '"sandbox.notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8404'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"https://sandbox.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://sandbox.zalopay.com.vn/"'
        }

        internalStaging {
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_INTERNAL'
            buildConfigField 'String', 'HOST', '"https://stg.zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.STAGING'
            buildConfigField 'String', 'WS_HOST', '"stg.notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8424'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"http://stgupload.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://stgrpe.zalopay.com.vn/"'
        }

        iapSandbox {
            // IAP dev with sandbox environment
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_EXTERNAL'
            buildConfigField 'String', 'HOST', '"https://sandbox.zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.SANDBOX'
            buildConfigField 'String', 'WS_HOST', '"sandbox.notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8404'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"https://sandbox.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://sandbox.zalopay.com.vn/"'
        }

        iapProduction {
            // IAP dev with live environment
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_EXTERNAL'
            buildConfigField 'String', 'HOST', '"https://zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.LIVE'
            buildConfigField 'String', 'WS_HOST', '"notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8414'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"http://upload.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://rpe.zalopay.com.vn/"'
        }

        iapStaging {
            // IAP dev with live environment
            buildConfigField "vn.com.vng.zalopay.config.ReactSupport", "REACT_DEVELOP_SUPPORT", 'vn.com.vng.zalopay.config.ReactSupport.DEV_EXTERNAL'
            buildConfigField 'String', 'HOST', '"https://stg.zalopay.com.vn/"'
            buildConfigField 'vn.com.zalopay.wallet.business.data.Constants.HostType', 'HOST_TYPE', 'vn.com.zalopay.wallet.business.data.Constants.HostType.STAGING'
            buildConfigField 'String', 'WS_HOST', '"stg.notify.zalopay.com.vn"'
            buildConfigField 'int', 'WS_PORT', '8424'
            buildConfigField 'String', 'UPLOAD_PHOTO_HOST', '"http://stgupload.zalopay.com.vn/"'
            buildConfigField 'String', 'REDPACKET_HOST', '"https://stgrpe.zalopay.com.vn/"'
        }
    }


    lintOptions {

        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'LICENSE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/maven/jaxen/jaxen/pom.properties'
        exclude 'META-INF/maven/jaxen/jaxen/pom.xml'
        exclude 'META-INF/INDEX.LIST'
        exclude "META-INF/io.netty.versions.properties"
        exclude 'META-INF/maven/com.google.zxing/core/pom.xml'
        exclude 'META-INF/maven/com.google.zxing/core/pom.properties'

    }

    dexOptions {
        jumboMode = true

        preDexLibraries = false
        javaMaxHeapSize "3g"
    }

    if (isCIBuild()) {
        // http://stackoverflow.com/questions/31191898/android-studio-gradle-dynamic-apk-name-out-of-sync/34095961#34095961
        // ./gradlew clean assemble -PCI_BUILD=true
        def clientBuildDate = new Date().format('yyyyMMddHHmm')
        // def newName = defaultConfig.versionName.replace(".", "_") + "-" + clientBuildDate
        def newName = "v" + defaultConfig.versionName
        def releaseDir = new File(getProjectDir(), "../../releases");
        println("release dir: " + releaseDir.absolutePath);
        releaseDir.mkdirs();

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(releaseDir.absolutePath, output.outputFile.name.replace(".apk", "-" + newName + ".apk").replace("app-", "ZaloPay-"));
            }
        }
    }
}

def isCIBuild() {
    return System.getenv('CI_BUILD') != null || hasProperty('CI_BUILD');
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd') //HHmmss
    return formattedDate
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += '--multi-dex'
        dx.additionalParameters += "--main-dex-list=$projectDir/zalopay".toString()
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def tests = rootProject.ext.testingLibraries
    def libs = rootProject.ext.libraries
    def devel = rootProject.ext.developmentDependencies;

    def cfgAndroid = rootProject.ext.configurationAndroid;

    testCompile tests.junit
    testCompile tests.robolectric

    compile cfgAndroid.multidex

    compile libs.butterKnife
    apt libs.butterKnifeCompiler

    debugCompile devel.leakCanaryDebug
    releaseCompile devel.leakCanaryRelease
    testCompile devel.leakCanaryTest

    compile libs.support_annotations
    compile libs.dagger
    apt libs.daggerCompiler
    provided libs.javaxAnnotation

    compile libs.play_services_gcm
    compile libs.play_services_analytics
    compile(libs.crashlytics) {
        transitive = true;
    }

    compile libs.parceler
    apt libs.parceler_apt

    debugCompile libs.debugViewer
    releaseCompile libs.debugViewer_noops
    compile libs.zalopay_analytics
    compile ('vn.com.vng.zalopay:qrcodecore:1.0')
    compile ('vn.com.vng.zalopay:rnmodules:1.3') {
        exclude module: 'bolts-android'
    }
    compile (libs.react_native) {
        exclude module: 'bolts-android'
    }
    compile ('com.zalo:zalosdk:2.1.0612')

    compile(name: 'WALLET-release', ext: 'aar')
    compile(name: 'soundtranscoder-release', ext: 'aar')

    debugCompile project(':zalopay.debug')
    compile project(':data')
    compile project(':gamemodule')
    compile project(':ui.widget')
    compile (project(':app-loader')) {
        exclude group: 'com.parse.bolts', module: 'bolts-android'
    }
}

apply plugin: 'com.google.gms.google-services'


